plugins {
	id 'java'
	id 'maven'
	id 'distribution'
	id 'org.dm.bundle' version '0.10.0'
}

//mainClassName = "org.daisy.dotify.devtools.osgi.OSGiLauncher"
//startScripts.applicationName = 'devtoolsgui'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = "org.daisy.dotify"
version = "1.0.0-SNAPSHOT"

sourceSets {
	main {
		java { srcDir 'src' exclude '**/resource-files/**/*'}
		resources { srcDir 'src' include '**/resource-files/**/*'}
	}
	test {
		java { srcDir 'test' exclude '**/resource-files/**/*'}
		resources { srcDir 'test' include '**/resource-files/**/*'} 
	}
	all {
		tasks[processResourcesTaskName].includeEmptyDirs = false
	}
}

configurations {
	launcher
}


repositories {
    mavenCentral()
    //mavenLocal()
    maven { url "https://oss.sonatype.org/content/groups/staging" }
    //maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
	compileOnly 'org.osgi:org.osgi.service.component.annotations:1.3.0'
	compile group: 'org.daisy.braille', name:'braille-utils.api', version:'4.3.1'
	compile group: 'org.daisy.braille', name:'braille-utils.impl', version:'4.1.0'
	compile group: 'org.daisy.braille', name:'braille-utils.pef-tools', version:'3.1.0'
	
	
	compile 'org.daisy.dotify:dotify.api:4.1.1'
	compile 'org.daisy.streamline:streamline-api:1.2.0'
	compile ('org.daisy.streamline:streamline-engine:1.2.0') {
		exclude module: 'streamline-api'
	}
	
	compile 'org.daisy.dotify:dotify.devtools:1.0.0-SNAPSHOT'
	runtime group: group, name:'dotify.hyphenator.impl', version:'4.0.0'
	runtime group: group, name:'dotify.formatter.impl', version:'4.3.0'
	runtime group: group, name:'dotify.text.impl', version:'4.0.0'
	runtime group: group, name:'dotify.translator.impl', version:'4.0.0'
	runtime ('org.daisy.dotify:dotify.task.impl:4.4.0') {
		exclude module: 'Saxon-HE'
	}
	
	runtime group: "com.googlecode.texhyphj", name: "texhyphj", version: "1.2"
	
	//compile group: 'org.eclipse.osgi', name: 'org.eclipse.osgi', version:'3.7.1'
	compile("org.osgi:org.osgi.core:5.0.0")
	runtime("org.osgi:org.osgi.compendium:5.0.0")
	runtime("org.apache.felix:org.apache.felix.scr:1.6.2")
	runtime('org.osgi:org.osgi.enterprise:4.2.0')
	launcher('org.daisy.dotify:osgi-launcher:0.1.0-SNAPSHOT')
	launcher("org.apache.felix:org.apache.felix.framework:4.4.1")
}

task runOSGi(type: JavaExec, dependsOn: 'installDist') {
	def path = "$buildDir/install/$project.name"
	main = "org.daisy.dotify.devtools.osgi.OSGiLauncher"
	//workingDir = "$path"
	classpath = files {file("$path/framework").listFiles() } //configurations.runtime + jar.outputs.files.getFiles()
	args "$path/bundles";
}

clean.doFirst {
	delete 'felix-cache'
}

bundle {
    instructions << [
		'-sources': 'true',
		'Private-Package': 'org.daisy.dotify.devtools.gui',
		'Service-Component': '*',
		'Bundle-Activator': 'org.daisy.dotify.devtools.gui.GuiActivator',
		'Include-Resource': 'LICENSE, NOTICE'	
    ]
}

installDist.dependsOn('jar')

distributions {
	main {
		contents {
			from (configurations.runtime) {
				into 'bundles'
			}
			from (jar.outputs.files.getFiles()) {
				into 'bundles'
			}
			from (configurations.launcher) {
				into 'framework'
			}
		}
	}
}

wrapper {
	gradleVersion = '4.5.1'
	distributionType = Wrapper.DistributionType.ALL
}