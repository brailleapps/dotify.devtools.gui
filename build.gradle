// For bnd
buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:4.1.0'
	}
}

plugins {
	id 'java'
	id 'maven-publish'
	id 'distribution'
}

apply plugin: 'biz.aQute.bnd.builder'

//mainClassName = "org.daisy.dotify.devtools.osgi.OSGiLauncher"
//startScripts.applicationName = 'devtoolsgui'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = "org.daisy.dotify"
version = "1.0.0-SNAPSHOT"

sourceSets {
	main {
		java { srcDir 'src' exclude '**/resource-files/**/*'}
		resources { srcDir 'src' include '**/resource-files/**/*'}
	}
	test {
		java { srcDir 'test' exclude '**/resource-files/**/*'}
		resources { srcDir 'test' include '**/resource-files/**/*'} 
	}
	all {
		tasks[processResourcesTaskName].includeEmptyDirs = false
	}
}

configurations {
	launcher
}


repositories {
    mavenCentral()
    //mavenLocal()
    maven { url "https://oss.sonatype.org/content/groups/staging" }
    //maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
	compileOnly 'org.osgi:org.osgi.service.component.annotations:1.3.0'
	implementation group: 'org.daisy.braille', name:'braille-utils.impl', version:'6.0.0'
	implementation group: 'org.daisy.braille', name:'braille-utils.pef-tools', version:'5.0.0'

	implementation 'org.daisy.dotify:dotify.api:4.4.0'
	implementation 'org.daisy.streamline:streamline-api:1.2.0'
	implementation ('org.daisy.streamline:streamline-engine:1.2.0') {
		exclude module: 'streamline-api'
	}
	
	implementation 'org.daisy.dotify:dotify.devtools:1.0.0-SNAPSHOT'
	runtimeOnly group: group, name:'dotify.hyphenator.impl', version:'4.0.0'
	runtimeOnly group: group, name:'dotify.formatter.impl', version:'4.4.0'
	runtimeOnly group: group, name:'dotify.text.impl', version:'4.0.0'
	runtimeOnly group: group, name:'dotify.translator.impl', version:'4.0.0'
	runtimeOnly ('org.daisy.dotify:dotify.task.impl:4.4.0') {
		exclude module: 'Saxon-HE'
	}
	
	runtimeOnly group: "com.googlecode.texhyphj", name: "texhyphj", version: "1.2"
	
	implementation("org.osgi:org.osgi.core:5.0.0")
	runtimeOnly("org.osgi:org.osgi.compendium:5.0.0")
	runtimeOnly("org.apache.felix:org.apache.felix.scr:2.1.14")
	runtimeOnly('org.osgi:org.osgi.enterprise:4.2.0')
	launcher('org.daisy.dotify:osgi-launcher:0.1.0-SNAPSHOT')
	launcher("org.apache.felix:org.apache.felix.framework:5.6.10")
}

task runOSGi(type: JavaExec, dependsOn: 'installDist') {
	def path = "$buildDir/install/$project.name"
	main = "org.daisy.dotify.devtools.osgi.OSGiLauncher"
	//workingDir = "$path"
	classpath = files {file("$path/framework").listFiles() } //configurations.runtimeOnly + jar.outputs.files.getFiles()
	args "$path/bundles";
}

clean.doFirst {
	delete 'felix-cache'
}

jar {
	manifest {
		attributes(
			'-sources': 'true',
			//'Private-Package': 'org.daisy.dotify.devtools.gui',
			'Bundle-Name': 'org.daisy.dotify.devtools.gui',
			'Bundle-SymbolicName': 'org.daisy.dotify.devtools.gui',
			'Bundle-Activator': 'org.daisy.dotify.devtools.gui.GuiActivator',
			'Include-Resource': 'LICENSE, NOTICE'	
		)
	}
}

installDist.dependsOn('jar')

distributions {
	main {
		contents {
			from (configurations.runtimeClasspath) {
				into 'bundles'
			}
			from (jar.outputs.files.getFiles()) {
				into 'bundles'
			}
			from (configurations.launcher) {
				into 'framework'
			}
		}
	}
}

wrapper {
	distributionType = Wrapper.DistributionType.ALL
}